buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.eriwen:gradle-js-plugin:1.4.0'
    }
}

repositories {
	mavenCentral()
}

apply plugin: 'war'
apply plugin: 'js'
apply plugin: 'jetty'

sourceCompatibility = 1.6
targetCompatibility = 1.6

ext {
	spring_version = "3.1.1.RELEASE"
	slf4j_version  = "1.7.2"
}

tasks.withType(Compile) {
    options.compilerArgs << "-Xlint:-options,-unchecked"
}

dependencies {
	compile "cglib:cglib-nodep:2.1_3"
	compile "commons-dbcp:commons-dbcp:1.4"
	compile "commons-io:commons-io:2.1"
	compile "commons-lang:commons-lang:2.6"
	compile "joda-time:joda-time:2.1"
	compile "jstl:jstl:1.2"
	compile "log4j:log4j:1.2.17"
	compile "org.codehaus.jackson:jackson-mapper-asl:1.9.9"
	compile "org.hibernate:hibernate-entitymanager:4.1.9.Final"
	compile "org.slf4j:slf4j-api:${slf4j_version}"
	compile "org.slf4j:slf4j-log4j12:${slf4j_version}"
	compile "org.slf4j:jcl-over-slf4j:${slf4j_version}"
    compile "org.springframework:spring-core:${spring_version}"
    compile "org.springframework:spring-orm:${spring_version}"
    compile "org.springframework:spring-tx:${spring_version}"
    compile "org.springframework:spring-webmvc:${spring_version}"

	runtime "com.h2database:h2:1.3.170"
	runtime "mysql:mysql-connector-java:5.1.22"

	testCompile "junit:junit-dep:4.11"
	testCompile "org.mockito:mockito-core:1.9.5"
	testCompile "org.hamcrest:hamcrest-library:1.3"
	testCompile "org.springframework:spring-test:${spring_version}"
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.4'
}

javascript.source {
    dev {
        js {
            srcDir "src/main/webapp/js"
            include "*.js"
            exclude "*.min.js"
        }
    }
    prod {
        js {
            srcDir "src/main/webapp/js"
            include "*.min.js"
        }
    }
}

task hintjs(type: com.eriwen.gradle.js.tasks.JsHintTask) {
    source = javascript.source.dev.js.files
    dest = file("${buildDir}/jshint.out")
    jshint.options = [unused:true, trailing:true, quotmark:"double", noempty:true, newcap:true, latedef:true]
}

jettyRun.doFirst {
    System.setProperty('xmlDataFile', 'Data.local.xml,Data.xml')
}

